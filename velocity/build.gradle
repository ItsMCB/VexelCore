plugins {
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

repositories {
    mavenCentral()
    // Velocity
    maven {
        name = 'velocitypowered-repo'
        url = 'https://repo.velocitypowered.com/releases/'
    }
    // Minecraft
    maven {
        name = 'minecraft-libraries'
        url = 'https://libraries.minecraft.net/'
    }
}

dependencies {
    implementation project(path: ':common')
    compileOnly 'com.velocitypowered:velocity-api:3.0.1'
    annotationProcessor 'com.velocitypowered:velocity-api:3.0.1'
    implementation "dev.dejvokep:boosted-yaml:1.3"
}

// Add gradle project version to each version placeholder in sources
processResources {
    filter { String line -> line.replace('${version}', project.version.toString()) }
}

task processSources(type: Sync) {
    from sourceSets.main.java.srcDirs
    filter { String line -> line.replace('${version}', project.version.toString()) }
    into "$buildDir/src"
}

compileJava.source = processSources.outputs


shadowJar {
    archiveFileName = 'VexelCore-Velocity-' + project.version.toString() + '.jar'
    relocate('me.itsmcb.vexelcore.common.api', 'me.itsmcb.vexelcore.common.api') {}
}

// Configure Auto Relocation

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "libs" // Default value is "shadow"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

artifacts {
    shadowJar
}

jar {
    enabled = false
    dependsOn(shadowJar)
}