plugins {
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

repositories {
    mavenCentral()
    // PaperMC (for Velocity)
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    // Minecraft
    maven {
        name = 'minecraft-libraries'
        url = 'https://libraries.minecraft.net/'
    }
}

dependencies {
    // VexelCore
    implementation project(path: ':common')
    // Velocity
    compileOnly("com.velocitypowered:velocity-api:3.1.1")
    annotationProcessor("com.velocitypowered:velocity-api:3.1.1")
    // Boosted YAML
    implementation "dev.dejvokep:boosted-yaml:1.3"
}

// Add gradle project version to each version placeholder in sources
processResources {
    filter { String line -> line.replace('${version}', project.version.toString()) }
}

task processSources(type: Sync) {
    from sourceSets.main.java.srcDirs
    filter { String line -> line.replace('${version}', project.version.toString()) }
    into "$buildDir/src"
}

compileJava.source = processSources.outputs


shadowJar {
    archiveFileName = 'VexelCore-Velocity-' + project.version.toString() + '.jar'
    relocate('me.itsmcb.vexelcore.common.api', 'me.itsmcb.vexelcore.common.api') {}
}

// Configure Auto Relocation

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "libs" // Default value is "shadow"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

artifacts {
    shadowJar
}

jar {
    enabled = false
    dependsOn(shadowJar)
}