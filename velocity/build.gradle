plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    mavenCentral()
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    implementation project(path: ':common')
    compileOnly 'com.velocitypowered:velocity-api:3.4.0-SNAPSHOT'
    annotationProcessor 'com.velocitypowered:velocity-api:3.4.0-SNAPSHOT'
    implementation "dev.dejvokep:boosted-yaml:1.3.6"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
}

processResources {
    filesMatching('**/*.yml') {
        expand project.properties
    }
}

tasks.register('processSources', Sync) {
    from sourceSets.main.java
    into "$buildDir/generated-src"
    filter { line -> line.replace('${version}', version.toString()) }
}

tasks.named('compileJava') {
    source = processSources.outputs
}

shadowJar {
    archiveFileName = "VexelCore-Velocity-${project.version}.jar"
    relocate 'me.itsmcb.vexelcore.common.api', 'me.itsmcb.vexelcore.common.api'
    // TODO Experiment with minimization at some point
    //minimize()
}

tasks.named('shadowJar') {
    enableRelocation true
    relocationPrefix "libs"
}

artifacts {
    archives shadowJar
}

tasks.named('jar') {
    enabled = false
    dependsOn(shadowJar)
}