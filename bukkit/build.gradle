plugins {
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    // PAPI
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    // FAWE and WorldGuard
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    // CoreProtect
    maven {
        url = "https://maven.playpro.com/"
    }
    maven { url "https://maven.enginehub.org/repo/" }
    // Geyser
    maven {
        url = uri("https://repo.opencollab.dev/main/")
    }
}

dependencies {
    implementation project(path: ':common')
    // Paper
    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"
    // WorldGuard
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.7'
    // FAWE
    implementation(platform("com.intellectualsites.bom:bom-1.18.x:1.20"))
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Core")
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Bukkit")
    // PlaceholderAPI
    compileOnly 'me.clip:placeholderapi:2.11.1'
    // CoreProtect
    compileOnly "net.coreprotect:coreprotect:21.3"

    compileOnly 'mysql:mysql-connector-java:8.0.33'
    implementation "dev.dejvokep:boosted-yaml-spigot:1.5"
    implementation "dev.dejvokep:boosted-yaml:1.3.6"
    // Geyser Floodgate
    compileOnly('org.geysermc.floodgate:api:2.2.2-SNAPSHOT')
}


def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

// Add gradle project version to each version placeholder in sources
processResources {
    filter { String line -> line.replace('${version}', project.version.toString()) }
}

task processSources(type: Sync) {
    from sourceSets.main.java.srcDirs
    filter { String line -> line.replace('${version}', project.version.toString()) }
    into "$buildDir/src"
}

compileJava.source = processSources.outputs

// Shadow config
shadowJar {
    archiveFileName = 'VexelCore-Bukkit-' + project.version.toString() + '.jar'
    relocate('me.itsmcb.vexelcore.common.api', 'me.itsmcb.vexelcore.common.api') {}
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "libs" // Default value is "shadow"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

artifacts {
    shadowJar
}

jar {
    enabled = false
    dependsOn(shadowJar)
}